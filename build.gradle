buildscript {
    ext {
        // ソースと実行ファイルのJava バージョン
        jdkVersion = jdkVersion
        defaultEncoding = defaultEncoding

        // SpringBoot
        springBootVersion = springbootVersion

        // springfox
        springfoxVersion = springfoxVersion

        // Others
        modelmapperVersion = modelmapperVersion

    }

    repositories {
        jcenter()
    }

}

plugins {
    id "com.github.ben-manes.versions" version "0.17.0"
    id 'com.github.hierynomus.license' version '0.14.0'
    id "com.github.spotbugs" version "1.6.1"
    id "org.springframework.boot" version "2.0.0.RELEASE"
    id "org.sonarqube" version "2.6.2"
}

allprojects {

    apply plugin: 'application'
    apply plugin: 'checkstyle'
    apply plugin: 'com.github.ben-manes.versions'
    apply plugin: 'com.github.hierynomus.license'
    apply plugin: 'com.github.spotbugs'
    apply plugin: 'idea'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'jacoco'
    apply plugin: 'java'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'pmd'
    apply plugin: 'war'

    repositories {
        jcenter()
    }

    checkstyle {
        toolVersion = '8.8'
        configFile = rootProject.file('config/checkstyle/google_checks.xml')
        showViolations = true
    }

    dependencies {

        // modelmapper
        compile("org.modelmapper.extensions:modelmapper-spring:${modelmapperVersion}")
        // lombok
        compile("org.projectlombok:lombok")

        // SpringBoot projects
        compile("org.springframework.boot:spring-boot-starter")
        compile('org.springframework.boot:spring-boot-starter-data-rest') {
            exclude group: 'org.springframework.boot', module: 'spring-boot-starter'
        }
        testCompile("org.springframework.boot:spring-boot-starter-test") {
            exclude group: 'org.springframework.boot', module: 'spring-boot-starter'
            exclude group: 'junit', module: 'junit'
        }

        compile("org.springframework.boot:spring-boot-starter-validation") {
            exclude group: 'org.springframework.boot', module: 'spring-boot-starter'
            exclude group: 'org.apache.tomcat.embed', module: 'tomcat-embed-el'
        }

        testCompile("org.springframework.restdocs:spring-restdocs-mockmvc")

        // SpringFox
        compile("io.springfox:springfox-bean-validators:$springfoxVersion")
        compile("io.springfox:springfox-data-rest:$springfoxVersion")
        compile("io.springfox:springfox-swagger2:$springfoxVersion")

        // Junit 5
        testCompile("org.junit.platform:junit-platform-launcher:1.1.0")
        testRuntimeOnly("org.junit.platform:junit-platform-engine:1.1.0")
        testImplementation("org.junit.jupiter:junit-jupiter-api")
        testImplementation("org.junit.jupiter:junit-jupiter-params:5.1.0") {
            exclude group: 'org.junit.jupiter', module: 'junit-jupiter-api'
        }
        testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine") {
            exclude group: 'org.junit.jupiter', module: 'junit-jupiter-api'
        }
    }

    jacoco {
        toolVersion = "0.8.0"
        reportsDir = file("$buildDir/customJacocoReportDir")
    }

    javadoc {
        options {
            addStringOption("-add-modules", "java.xml.bind,java.xml.ws.annotation")
            author = true
            charSet = defaultEncoding
            docEncoding = defaultEncoding
            encoding = defaultEncoding
            links(
                    "https://docs.oracle.com/javase/9/docs/api/",
                    "https://docs.spring.io/spring-boot/docs/current/api/"
            )
            linkSource = true
            noHelp = true
            setMemberLevel(JavadocMemberLevel.PUBLIC)
            setNoQualifiers(["all"])
        }
    }

    license {
        include "**/*.java"
        header = rootProject.file("config/license/license.template")
        strictCheck = true
        skipExistingHeaders = true
        ext {
            copyright_range = Calendar.getInstance().get(Calendar.YEAR)
            name = "urata"
        }
        mapping {
            java = "DOUBLESLASH_STYLE"
        }
        licenseMain.enabled = false
        licenseTest.enabled = false
    }

    pmd {
        toolVersion = "6.1.0"
        ignoreFailures = true
        consoleOutput = true
        ruleSetFiles = rootProject.files({ "/config/pmd/all-java.xml" })
        ruleSets = [
                'java-braces',
                'java-codesize',
        ]
    }

    spotbugs {
        toolVersion = '3.1.2'
    }

    task('default') {
        dependsOn 'clean', 'check'
    }

    tasks.withType(Checkstyle) {
        reports {
            xml.enabled = false
            html.enabled = true
            html.stylesheet = resources.text.fromFile(rootProject.file("config/checkstyle/checkstyle-custom.xsl"))
        }
    }

    tasks.withType(JavaCompile) {
        doFirst {
            options.encoding = defaultEncoding
            sourceCompatibility = jdkVersion
            targetCompatibility = jdkVersion
            options.compilerArgs = [
                    '-Xlint:unchecked', '-Xlint:deprecation',
                    '--add-modules', 'java.xml.bind',
                    '--add-modules', 'java.xml.ws.annotation',
                    '--add-exports', 'java.xml.bind/com.sun.istack.internal=ALL-UNNAMED'
            ]
        }
    }

    tasks.withType(com.github.spotbugs.SpotBugsTask) {
        reports {
            xml.enabled = false
            html.enabled = true
        }
    }

}

idea {
    project {
        languageLevel = jdkVersion
        vcs = "Git"
    }
}

sonarqube {
    properties {
        property "sonar.github.repository", "mabubu0203/ItemControl"
        property "sonar.host.url", "http://localhost:9000/"
        property "sonar.jacoco.reportPaths", { "${project.buildDir}/jacoco/test.exec" }
        property "sonar.jdbc.driverClassName", "com.mysql.jdbc.Driver"
        property "sonar.junit.reportsPaths", { "${project.buildDir}/test-results" }
        property "sonar.language", "java"
        property "sonar.links.issue", "https://github.com/mabubu0203/ItemControl/issues"
        property "sonar.links.scm", "https://github.com/mabubu0203/ItemControl"
        property "sonar.scm.provider", "git"
    }
}

project(':micro-api') {
    ext.moduleName = 'jp.co.valtech.items.api'
    dependencies {
        compile project(':micro-common'), project(':micro-interfaces'), project(':micro-rdb')
    }
}

project(':micro-common') {
    ext.moduleName = 'jp.co.valtech.items.common'
}

project(':micro-interfaces') {
    ext.moduleName = 'jp.co.valtech.items.interfaces'
    dependencies {
        compile project(':micro-test')
    }
}

project(':micro-rdb') {
    ext.moduleName = 'jp.co.valtech.items.rdb'
    dependencies {
        compile project(':micro-common'), project(':micro-interfaces')
    }
}
project(':micro-test') {
    ext.moduleName = 'jp.co.valtech.items.test'
}

task wrapper(type: Wrapper) {
    gradleVersion = gradleVersion
}